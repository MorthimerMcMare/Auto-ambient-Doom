class AutoAmbientSoundDefinition {
	String name;
	Sound snd;
	EAutoAmbientSoundModes sndMode;
	double sndVolume, sndAttenuation;
	uint minDelay, maxDelay;

	double minRadius, minHeight;

	Array<AutoAmbientGroup> groups;

	/*uint workDistance;
	Sound workSound;
	double workSoundVolume, workSoundAttenuation;*/

	static AutoAmbientSoundDefinition Create( String initName, Sound initSnd, EAutoAmbientSoundModes initMode, double initVolume, double initAttn, uint initMinDelay, uint initMaxDelay ) {
		AutoAmbientSoundDefinition newDef = new( 'AutoAmbientSoundDefinition' );

		newDef.name = initName;
		newDef.snd = initSnd;
		newDef.sndMode = initMode;
		newDef.sndVolume = initVolume;
		newDef.sndAttenuation = initAttn;
		newDef.minDelay = initMinDelay;
		newDef.maxDelay = initMaxDelay;

		newDef.minRadius = newDef.minHeight = 0.0;
		/*newDef.workDistance = -1;
		newDef.workSoundVolume = newDef.workSoundAttenuation = 1.0;
		newDef.workSound = "";*/

		return newDef;
	}
} // of class AutoAmbientSoundDefinition {}


class AutoAmbientCacheTexture {
	TextureID texID;
	AutoAmbientTexture texdef;
	uint frequency;
} // of class AutoAmbientCacheTexture {}

class AutoAmbientTextureParameter {
	const defaultStart = -50.0;
	const defaultOffset = -100.0;

	vector2 start, offset, spreading;
	double chance;
	String name;

	static AutoAmbientTextureParameter Create( String paramname, vector2 initStartOffset, vector2 initOffset, vector2 initSpreading, double initChance ) {
		AutoAmbientTextureParameter newDef = new( 'AutoAmbientTextureParameter' );

		// To prevent an infinite loop in actors spawning.
		if ( initOffset.x >= 0 && initOffset.x < 1.0 ) {
			initOffset.x = defaultOffset;
			// Temporal solution.
			console.printf( "AutoAmbientTextureParameter::Create(). Warning: offset X in texture parameter \"" .. paramname .. "\" is too small (" .. initOffset.x .. "), setting to the default." );
		}
		if ( initOffset.y >= 0 && initOffset.y < 1.0 ) {
			initOffset.y = defaultOffset;
			console.printf( "AutoAmbientTextureParameter::Create(). Warning: offset Y in texture parameter \"" .. paramname .. "\" is too small (" .. initOffset.y .. "), setting to the default." );
		}

		newDef.name = paramname;
		newDef.start = initStartOffset;
		newDef.offset = initOffset;
		newDef.spreading = initSpreading;
		newDef.chance = ( initChance >= 0.0 && initChance <= 1.0 )? initChance : 1.0;

		return newDef;
	}

	// To enforce AutoAmbientTexture to recalculate offsets based on size.
	AutoAmbientTextureParameter Copy( void ) const {
		AutoAmbientTextureParameter copiedDef = new( 'AutoAmbientTextureParameter' );

		copiedDef.name = name;
		copiedDef.start = start;
		copiedDef.offset = offset;
		copiedDef.spreading = spreading;
		copiedDef.chance = chance;

		return copiedDef;
	}
} // of class AutoAmbientTextureParameter {}


class AutoAmbientTexture {
	TextureID id;
	vector2 size; // Texture precalculated size.

	bool invalidFlag;

	Array<AutoAmbientGroup> groups;
	Array<AutoAmbientTextureParameter> groupparams;

	EAutoAmbientLinesCheckFlags linesCheckFlags;


	static AutoAmbientTexture Create( String textureName ) {
		AutoAmbientTexture newTex = NULL;
		TextureID addeeTexID = TexMan.CheckForTexture( textureName, TexMan.Type_Wall, TexMan.DontCreate );

		if ( TexMan.GetName( addeeTexID ) != "" ) {
			newTex = new( 'AutoAmbientTexture' );

			newTex.id = addeeTexID;
			newTex.size = TexMan.GetScaledSize( addeeTexID );
			newTex.linesCheckFlags = AALCF_Default;
			newTex.invalidFlag = false;
		}

		return newTex;
	} // of static AutoAmbientTexture Create( String textureName ) {}

	static AutoAmbientTexture CreateTemporalBase( void ) {
		AutoAmbientTexture newTex = new( 'AutoAmbientTexture' );

		newTex.linesCheckFlags = AALCF_Default;

		return newTex;
	}

	void AddGroupParam( AutoAmbientGroup group, AutoAmbientTextureParameter param ) {
		if ( group && param ) {
			groups.Push( group );
			groupparams.Push( param );

			if ( size.length() > 0 ) {
				if ( param.start.x < 0.0 )
					param.start.x = size.x * -param.start.x / 100.0;
				if ( param.start.y < 0.0 )
					param.start.y = size.y * -param.start.y / 100.0;

				if ( param.offset.x <= 0.0 )
					param.offset.x = size.x * -param.offset.x / 100.0;
				if ( param.offset.y <= 0.0 )
					param.offset.y = size.y * -param.offset.y / 100.0;

				if ( param.spreading.x <= 0.0 )
					param.spreading.x = size.x * -param.spreading.x / 100.0;
				if ( param.spreading.y <= 0.0 )
					param.spreading.y = size.y * -param.spreading.y / 100.0;
			}
		} else {
			invalidFlag = true;
		}
	} // of void AddGroupParam( AutoAmbientGroup group, AutoAmbientTextureParameter param ) {}

	// By default start point is in the centre of the texture and 
	//offset is a one texture size (for both X and Y):
	void AddGroup( AutoAmbientGroup group, vector2 xy = ( AutoAmbientTextureParameter.defaultStart, AutoAmbientTextureParameter.defaultStart ) ) {
		AddGroupParam( group,
			AutoAmbientTextureParameter.Create(
					"__auto_texparam", xy,
					( AutoAmbientTextureParameter.defaultOffset, AutoAmbientTextureParameter.defaultOffset ),
					( 0, 0 ), 1.0
			)
		);
	}

	bool Validate( void ) {
		return ( id && size.Length() && groups.Size() && !invalidFlag );
	}
} // of class AutoAmbientTexture {}
