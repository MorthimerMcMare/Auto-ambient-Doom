// Utils, global classes and extendings.

class AutoAmbientLogger: Thinker {
	const LLCOLOR_DEBUG = TEXTCOLOR_DARKGRAY;
	const LLCOLOR_EMERGENCY = TEXTCOLOR_FIRE;

	int aaCurLogLevel;

	static void UpdateLogLevel( void ) {
		AutoAmbientLogger.Get().aaCurLogLevel = CVar.FindCVar( "mcm_aambient_loglevel" ).GetInt();
	}

	static play AutoAmbientLogger Get( void ) {
		AutoAmbientLogger it = AutoAmbientLogger( ThinkerIterator.Create( "AutoAmbientLogger", STAT_STATIC ).Next() );

		if ( !it ) {
			// Creating and initializating a singleton global object:
			it = new( "AutoAmbientLogger" );
			it.ChangeStatNum( STAT_STATIC );
			it.UpdateLogLevel(); // This one calls "Get()" again, yeah...
		}

		return it;
	} // of static play AutoAmbientLogger Get( void ) {}


	// Prints well-decorated information string. "LL_NoPrefix" also disables
	//default color mark:
	static clearscope void ClearscopeLog( EAutoAmbientLogLevels loglevel, String text ) {
		String logPrefix = "";

		if ( !( loglevel & LL_NoPrefix ) ) {
			switch ( loglevel & LL__No_Flags_Mask ) {
				case LL_Debug:
					logPrefix = LLCOLOR_DEBUG .. "[AA Debug] ";
					break;
				case LL_Emergency:
					logPrefix = TEXTCOLOR_RED .. "[AutoAmbient emergency] " .. LLCOLOR_EMERGENCY;
					break;
				default:
					logPrefix = TEXTCOLOR_GRAY .. "[AA] \c-";
					break;
			}
		} // of if ( !( loglevel & LL_NoPrefix ) ) {}

		String lastSymbol = text.Mid( text.Length() - 1 );
		bool extraDot = !( lastSymbol == "." || lastSymbol == "!" || lastSymbol == "?" || lastSymbol == ";" || lastSymbol == ":" );

		console.printf( logPrefix .. text .. ( extraDot? "." : "" ) );
	} // of static clearscope void ClearscopeLog( EAAmbientLogLevels loglevel, String text ) {}

	// A play-scope wrapper around a data-scope method (mostly used):
	static play void Log( EAutoAmbientLogLevels loglevel, String text ) {
		if ( AutoAmbientLogger.Get().aaCurLogLevel >= ( loglevel & LL__No_Flags_Mask ) )
			ClearscopeLog( loglevel, text );
	}

} // of class AutoAmbientLogger: Thinker {}


class AutoAmbientUtil {
	static double vector2ToAngle( vector2 vec ) {
		double outAngle;

		if ( vec.x == 0.0 )
			vec.x = 0.00001;
		outAngle = atan( vec.y / vec.x );

		if ( vec.x < 0 )
			outAngle = outAngle - 180.0;

		return outAngle;
	} // of static double vector2ToAngle( vector2 vec ) {}


    static String squeezeVector3( vector3 vec ) {
        return "(" .. int( vec.x ) .. ", " .. int( vec.y ) .. ", " .. int( vec.z ) .. ")";
    }
} // of class AutoAmbientUtil {}




extend class AutoAmbientGlobalHandler {
	String debugTextureDataPrefix;
	AutoAmbientLogger debugLogger;


	// Static event handlers (and its finders) has quite confusing behaviour,
	//so it's better to set pointer to this class manually.
	static AutoAmbientGlobalHandler Get( void ) {
		return AutoAmbientGlobalHandler( StaticEventHandler.Find( "AutoAmbientGlobalHandler" ) );
	}



	bool DescendIncludeFileLevel( void ) {
		bool outValue = false;

		if ( replicateParserAmount < replicateParserMaxAmount ) {
			replicateParserAmount++;
			outValue = true;
		}

		return outValue;
	}

	void AscendIncludeFileLevel( void ) {
		if ( replicateParserAmount > 0 )
			replicateParserAmount--;
		else
			AutoAmbientLogger.Get().Log( LL_Emergency, GetClassName() .. "AscendIncludeFileLevel(). Tried to ascend over the first level." );
	}

	String GetFormattedIncludeFileLevel( void ) {
		return TEXTCOLOR_GRAY .. ( ( replicateParserAmount == 0 )? "top" : "nested level " .. replicateParserAmount .. "\c-" );
	}


	static clearscope int GetPreviousLumpGlobalPos( String baseLumpName, int baseLumpHander ) {
		int outPos = AAParser_NoGlobalLocating;

		for ( int i = 0; i < 99; i++ ) {
			int compLumpHandler = Wads.CheckNumForName( baseLumpName, Wads.ns_global, i );

			if ( compLumpHandler == -1 ) {
				break;
			} else if ( baseLumpHander == compLumpHandler ) {
				outPos = ( i == 0? AAParser_NoGlobalLocating : i );
				break;
			}
		}

		return outPos;
	} // of int GetPreviousLumpGlobalPos( String baseLumpHander ) {}


	protected AutoAmbientTexture FindInvalidTexture( void ) {
		for ( int i = 0; i < textures.Size(); i++ )
			if ( !textures[ i ].Validate() )
				return textures[ i ];

		return NULL;
	}

	protected bool IsAutoAmbientEnabled( void ) {
		CVar AAEnabled = CVar.FindCVar( "mcm_aambient_enabled" );
		return ( AAEnabled && AAEnabled.GetBool() );
	}


	private void printDebugTextureData( uint texindex, AutoAmbientTexture curtex ) {
		if ( curtex ) {
			debugLogger.ClearscopeLog( LL_Debug, debugTextureDataPrefix .. "TEX #" .. texindex .. " \"" .. TEXTCOLOR_TEAL .. curtex.id .. AutoAmbientLogger.LLCOLOR_DEBUG .. "\": size " .. curtex.size .. ", flags " .. curtex.linesCheckFlags .. ", spawnchances same:" .. curtex.sameLineDistSpawnChance .. "/oth:" .. curtex.otherLineDistSpawnChance .. "; groups:" );
			String debugStr = "";

			for ( int j = 0; j < curtex.groups.Size(); j++ ) {
				debugStr = debugStr .. " (" .. TEXTCOLOR_WHITE;

				if ( !curtex.groups[ j ] )
					debugStr = debugStr .. debugLogger.LLCOLOR_EMERGENCY .. "Group #" .. j .. " is NULL!";
				else if ( !curtex.groupparams[ j ] )
					debugStr = debugStr .. debugLogger.LLCOLOR_EMERGENCY .. "Group \"" .. curtex.groups[ j ].name .. "\" (#" .. j .. ")'s texparam is NULL!!!";
				else
					debugStr = debugStr .. curtex.groups[ j ].name .. TEXTCOLOR_DARKGRAY .. " at " .. TEXTCOLOR_OLIVE .. curtex.groupparams[ j ].name;

				debugStr = debugStr .. debugLogger.LLCOLOR_DEBUG .. ")";
			}

			if ( debugStr == "" )
				debugStr = TEXTCOLOR_YELLOW .. " No groups defined";

			debugLogger.ClearscopeLog( LL_Debug, debugTextureDataPrefix .. " \\_" .. debugStr );
		} else {
			debugLogger.ClearscopeLog( LL_Emergency, debugTextureDataPrefix .. "TEX #" .. texindex .. ": NULL!!!" );
		}
	}

	private void OnRegisterDebugFullOutput( void ) {
		// You cannot call a function with an Array<> argument type in some
		//old versions of the engine.

		debugLogger = AutoAmbientLogger.Get();

		debugLogger.ClearscopeLog( LL_Debug, TEXTCOLOR_SAPPHIRE .. GetClassName() .. "::OnRegister(). Sound definitions (total " .. sounddefs.Size() .. ")" .. debugLogger.LLCOLOR_DEBUG .. ":" );
		for ( int i = 0; i < sounddefs.Size(); i++ ) {
			if ( sounddefs[ i ] ) {
				String soundmodeStr;

				switch ( sounddefs[ i ].sndMode ) {
					case AASM_Looped:
						soundmodeStr = "looped";
						break;
					case AASM_Fixed:
						soundmodeStr = "fixed delay {" .. sounddefs[ i ].minDelay .. "}";
						break;
					case AASM_Random:
						soundmodeStr = "random delay [" .. sounddefs[ i ].minDelay .. ".." .. sounddefs[ i ].maxDelay .. "]";
						break;
					default:
						soundmodeStr = debugLogger.LLCOLOR_EMERGENCY .. "unknown";
						break;
				}

				debugLogger.ClearscopeLog( LL_Debug, "SND #" .. i .. " \"" .. TEXTCOLOR_GRAY .. sounddefs[ i ].name .. debugLogger.LLCOLOR_DEBUG .. "\": snd " .. sounddefs[ i ].snd .. " vol " .. sounddefs[ i ].sndVolume .. " attn " .. sounddefs[ i ].sndAttenuation .. ", " .. soundmodeStr .. " mode" );
			} else {
				debugLogger.ClearscopeLog( LL_Emergency, "SND #" .. i .. ": NULL!!!" );
			}
		} // of for ( int i = 0; i < sounddefs.Size(); i++ ) {}

		debugLogger.ClearscopeLog( LL_Debug, TEXTCOLOR_SAPPHIRE .. GetClassName() .. "::OnRegister(). Texture parameters (total " .. textureparams.Size() .. ")" .. debugLogger.LLCOLOR_DEBUG .. ":" );
		for ( int i = 0; i < textureparams.Size(); i++ ) {
			if ( textureparams[ i ] )
				debugLogger.ClearscopeLog( LL_Debug, "TPM #" .. i .. " \"" .. TEXTCOLOR_GRAY .. textureparams[ i ].name .. debugLogger.LLCOLOR_DEBUG .. "\": " .. textureparams[ i ].start .. "++" .. textureparams[ i ].offset .. " rnd " .. textureparams[ i ].spreading .. " chnc " .. textureparams[ i ].chance );
			else
				debugLogger.ClearscopeLog( LL_Emergency, "TPM #" .. i .. ": NULL!!!" );
		} // of for ( int i = 0; i < textureparams.Size(); i++ ) {}

		debugLogger.ClearscopeLog( LL_Debug, TEXTCOLOR_SAPPHIRE .. GetClassName() .. "::OnRegister(). Groups (total " .. groups.Size() .. ")" .. debugLogger.LLCOLOR_DEBUG .. ":" );
		for ( int i = 0; i < groups.Size(); i++ ) {
			AutoAmbientGroup curgroup = groups[ i ];

			if ( curgroup ) {
				String debugStr = String.Format( "GRP #%i \"%s\": mindist %.3f, gridX.Size() %i, %s, %s; classes:",
								i, curgroup.name, curgroup.actorsDistance, curgroup.gridX.Size(),
								( curgroup.spawned? "&spawned exists" : TEXTCOLOR_FIRE .. "no &spawned" .. debugLogger.LLCOLOR_DEBUG ),
								( curgroup.spawnedOnSide? "&spawnedOnSide exists" : TEXTCOLOR_FIRE .. "no &spawnedOnSide" .. debugLogger.LLCOLOR_DEBUG ) );
				debugLogger.ClearscopeLog( LL_Debug, debugStr );

				debugStr = "";

				for ( int j = 0; j < curgroup.classes.Size(); j++ ) {
					debugStr = debugStr .. " (" .. TEXTCOLOR_GRAY;

					if ( !curgroup.classes[ j ] )
						debugStr = debugStr .. " (" .. debugLogger.LLCOLOR_EMERGENCY .. "Class #" .. j .. " is NULL!";
					else
						debugStr = debugStr .. curgroup.classes[ j ].GetClassName();

					debugStr = debugStr .. debugLogger.LLCOLOR_DEBUG .. ")";
				}

				for ( int j = 0; j < curgroup.autoclasses.Size(); j++ ) {
					debugStr = debugStr .. " (" .. TEXTCOLOR_GRAY;

					if ( !curgroup.autoclasses[ j ] )
						debugStr = debugStr .. " (" .. debugLogger.LLCOLOR_EMERGENCY .. "Autoclass #" .. j .. " is NULL!";
					else
						debugStr = debugStr .. "Auto:" .. curgroup.autoclasses[ j ].name;

					debugStr = debugStr .. debugLogger.LLCOLOR_DEBUG .. ")";
				}

				if ( debugStr == "" )
					debugStr = TEXTCOLOR_YELLOW .. " No classes defined";

				debugLogger.ClearscopeLog( LL_Debug, " \\_" .. debugStr );
			} else {
				debugLogger.ClearscopeLog( LL_Emergency, "GRP #" .. i .. ": NULL!!!" );
			}
		} // of for ( int i = 0; i < groups.Size(); i++ ) {}

		debugTextureDataPrefix = "";

		debugLogger.ClearscopeLog( LL_Debug, TEXTCOLOR_SAPPHIRE .. GetClassName() .. "::OnRegister(). Global textures (total " .. textures.Size() .. ")" .. debugLogger.LLCOLOR_DEBUG .. ":" );
		for ( int i = 0; i < textures.Size(); i++ )
			printDebugTextureData( i, textures[ i ] );

		if ( maptextures.Size() ) {
			bool isMapColor2 = false;

			debugLogger.ClearscopeLog( LL_Debug, TEXTCOLOR_SAPPHIRE .. GetClassName() .. "::OnRegister(). Map defines (total " .. maptextures.Size() .. ")" .. debugLogger.LLCOLOR_DEBUG .. ":" );

			for ( int i = 0; i < maptextures.Size(); i++ ) {
				String mapColor2 = ( isMapColor2? TEXTCOLOR_PURPLE : TEXTCOLOR_DARKGREEN );
				debugTextureDataPrefix = mapColor2 .. "> " .. AutoAmbientLogger.LLCOLOR_DEBUG;

				AutoAmbientMapsData curmap = maptextures.maps[ i ];
				debugLogger.ClearscopeLog( LL_Debug, mapColor2 .. "MAP #" .. i .. AutoAmbientLogger.LLCOLOR_DEBUG .. ": name \"" .. mapColor2 .. curmap.mapname .. AutoAmbientLogger.LLCOLOR_DEBUG .. "\"/hash \"" .. mapColor2 .. curmap.maphash .. AutoAmbientLogger.LLCOLOR_DEBUG .. "\", no dflt tex: " .. curmap.restrictDefaultTextures .. ", textures:" );

				for ( int j = 0; j < curmap.textures.Size(); j++ )
					printDebugTextureData( j, curmap.textures[ j ] );

				isMapColor2 = !isMapColor2;
			}
		} else {
			debugLogger.ClearscopeLog( LL_Debug, TEXTCOLOR_SAPPHIRE .. GetClassName() .. "::OnRegister(). No map defines." );
		}

	} // of private void OnRegisterDebugFullOutput( void ) {}


	protected void LocateLevelBorders( void ) {
		rightLevelBound = -32768.0;
		bottomLevelBound = 32768.0;
		topLevelBound = -32768.0;
		leftLevelBound = 32768.0;

		for ( int i = 0; i < level.Vertexes.Size(); i++ ) {
			vector2 vertexpos = level.Vertexes[ i ].p;
			if ( vertexpos.y > topLevelBound )
				topLevelBound = vertexpos.y;
			else if ( vertexpos.y < bottomLevelBound )
				bottomLevelBound = vertexpos.y;

			if ( vertexpos.x > rightLevelBound )
				rightLevelBound = vertexpos.x;
			else if ( vertexpos.x < leftLevelBound )
				leftLevelBound = vertexpos.x;
		}
	} // of protected void locateLevelBorders( void ) {}

} // of extend class AutoAmbientGlobalHandler {}
