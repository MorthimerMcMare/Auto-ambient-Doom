version "2.4"

class AutoAmbientGroup play {
	String name;
	Array< class<Actor> > classes;
	Array<TextureID> textures;
	double actorsDistance;
	double actorsSpreading;
	TextureID cachedTexture;

	Array<Actor> actors;

	static AutoAmbientGroup Create( String groupname, double dist, double spreading ) {
		AutoAmbientGroup newGroup = new( 'AutoAmbientGroup' );

		newGroup.name = groupname;
		newGroup.actorsDistance = dist;
		newGroup.actorsSpreading = spreading;
		//newGroup.cachedTexture = 0;

		return newGroup;
	}

	// See GZDoom source code, "/src/common/textures/textureid.h" ("class FTextureID").
	//TextureID comparing is really much faster than same for String.
	bool IsTextureInGroup( TextureID texID ) {
		// At least something (in my opinion about 20%) will be speeded up.
		if ( texID == cachedTexture )
			return true;

		for ( int i = 0; i < textures.Size(); i++ ) {
			if ( texID == textures[ i ] ) {
				cachedTexture = texID;
				return true;
			}
		}

		return false;
	} // of bool IsTextureInGroup( TextureID texID ) {

	bool AddClass( class<Actor> cls ) {
		if ( cls )
			classes.Push( cls );

		return !!cls;
	}

	bool AddTexture( String textureName ) {
		TextureID addeeTex = TexMan.CheckForTexture( textureName, TexMan.Type_Wall );

		if ( addeeTex ) {
			// Dirty hack. But ZScript cannot push TextureID to the 
			//Array<TextureID> due to special class logic.
			textures.Push( 0 );
			textures[ textures.Size() - 1 ] = addeeTex;
		}

		return !!addeeTex;
	}

	void SpawnActor( vector3 actorpos /*Line attachLine*/ ) {
		if ( classes.Size() ) {
			Actor newActor = Actor.Spawn( classes[ Random( 0, classes.Size() - 1 ) ], actorpos );
			bool grouplingTooClose = false;

			// Maybe it's better to get rid of this check from the main cycle.
			for ( int i = 0; i < actors.Size(); i++ ) {
				//console.printf( "#" .. i .. ". actors.Size(): " .. actors.Size() .. ", newActor: " .. newActor .. ", actors[ i ]: " .. actors[ i ]  );

				if ( newActor.Distance2D( actors[ i ] ) < actorsDistance ) {
					grouplingTooClose = true;
					break;
				}
			}

			if ( grouplingTooClose ) {
				newActor.Destroy();
			} else if ( newActor ) {
				if ( actorsSpreading != 0.0 )
					newActor.SetOrigin( newActor.pos + ( FRandom( -actorsSpreading, actorsSpreading ), FRandom( -actorsSpreading, actorsSpreading ), 0 ), false );

				actors.Push( newActor );
				console.printf( "Actor " .. newActor.GetClassName() .. " spawned at " .. newActor.pos );
			}
		} // of if ( classes.Size() ) {
	} // of void SpawnActor( vector3 pos ) {

} // of class AutoAmbientGroup {


class AutoAmbientGroupCache {
	TextureID texture;
	Array<AutoAmbientGroup> groups;
	uint frequency;
}

class AutoAmbientHandler: EventHandler {
	const AutoAmbientHandlerOrder = 0x7FAA0003;

	Array<AutoAmbientGroup> groups;
	Array<AutoAmbientGroupCache> groupcache; // Just a list ordered by use frequency.

	override void OnRegister() {
		SetOrder( AutoAmbientHandlerOrder );

		groups.Push( AutoAmbientGroup.Create( "Comp", 128, 32 ) );
		groups[ 0 ].AddClass( "AutoAmbientActor_Comp" );
		groups[ 0 ].AddTexture( "COMPSTA1" );
		groups[ 0 ].AddTexture( "COMPSTA2" );
		groups[ 0 ].AddTexture( "COMPTALL" );
		groups[ 0 ].AddTexture( "SILVER3" );
		groups[ 0 ].AddTexture( "SPACEW3" );

		groups.Push( AutoAmbientGroup.Create( "Pipe", 256, 0 ) );
		groups[ 1 ].AddClass( "AutoAmbientActor_Pipe" );
		groups[ 1 ].AddTexture( "BROWNPIP" );
		groups[ 1 ].AddTexture( "METAL4" );
		groups[ 1 ].AddTexture( "METAL4" );
		groups[ 1 ].AddTexture( "PIPE2" );
		groups[ 1 ].AddTexture( "PIPE6" );
		groups[ 1 ].AddTexture( "PIPEWAL1" );
		groups[ 1 ].AddTexture( "PIPES" );
		groups[ 1 ].AddTexture( "SW1PIPE" );
		groups[ 1 ].AddTexture( "SW2PIPE" );
		groups[ 1 ].AddTexture( "TEKGREN4" );
		groups[ 1 ].AddTexture( "ZZZFACE5" );
		groups[ 1 ].AddTexture( "ZZZFACE7" );
		groups[ 1 ].AddTexture( "ZZZFACE8" );

		groups.Push( AutoAmbientGroup.Create( "Tech", 192, 24 ) );
		groups[ 2 ].AddClass( "AutoAmbientActor_Tech" );
		groups[ 2 ].AddTexture( "CEMENT2" );
		groups[ 2 ].AddTexture( "CEMENT4" );
		groups[ 2 ].AddTexture( "ICKWALL4" );
		groups[ 2 ].AddTexture( "SW1COMP" );
		groups[ 2 ].AddTexture( "SW2COMP" );
		groups[ 2 ].AddTexture( "COMPTALL" );
		groups[ 2 ].AddTexture( "COMPWERD" );
		groups[ 2 ].AddTexture( "TEKGREN1" );
		groups[ 2 ].AddTexture( "TEKWALL1" );
		groups[ 2 ].AddTexture( "TEKWALL4" );
		groups[ 2 ].AddTexture( "TEKWALL6" );
		groups[ 2 ].AddTexture( "ZZZFACE6" );

		groups.Push( AutoAmbientGroup.Create( "BloodPipe", 128, 0 ) );
		groups[ 3 ].AddClass( "AutoAmbientActor_BloodPipe" );
		groups[ 3 ].AddTexture( "BLODRIP1" );
		groups[ 3 ].AddTexture( "BLODRIP2" );
		groups[ 3 ].AddTexture( "BLODRIP3" );
		groups[ 3 ].AddTexture( "BLODRIP4" );
		groups[ 3 ].AddTexture( "PIPE1" );
		groups[ 3 ].AddTexture( "SK_LEFT" );
		groups[ 3 ].AddTexture( "SK_RIGHT" );
		groups[ 3 ].AddTexture( "SLOPPY2" );

		Super.OnRegister();
	}

	override void WorldLoaded( WorldEvent e ) {

		// A-a-argh! Five nested "for" cycles in worst case!..
		for ( int curSideIndex = 0; curSideIndex < level.Sides.Size(); curSideIndex++ ) {
			Side l = level.Sides[ curSideIndex ];

			for ( int i = 0; i < 2; i++ ) {
				TextureID texID = l.GetTexture( i );

				if ( texID ) {
					//console.printf( "Texture \"" .. TexMan.GetName( texID ) .. "\"." );
					AutoAmbientGroupCache texCache = NULL;

					// Attempt to find texture in cache array:
					for ( int j = 0; j < groupcache.Size(); j++ ) {
						if ( groupcache[ j ].texture == texID ) {
							texCache = groupcache[ j ];
							groupcache[ j ].frequency++;

							// Ordering by use frequency:
							/*if ( j > 0 && groupcache[ j ].frequency > groupcache[ j - 1 ].frequency ) {
								// Really slow executing caused by universality.
								//Moreover, lines below will swap only neighbour
								//elements ("2, 1, 1, 2" -> "2, 1, 2, 1").
								groupcache.Insert( j - 1, groupcache[ j ] );
								groupcache.Delete( j + 1, 1 );
							}*/

							break;
						}
					}

					// Creating new texture cache if necessary:
					if ( !texCache ) {
						texCache = new( 'AutoAmbientGroupCache' );
						texCache.texture = texID;
						texCache.frequency = 1;

						// Different groups may handle one texture.
						for ( int j = 0; j < groups.Size(); j++ ) {
							if ( groups[ j ].IsTextureInGroup( texID ) )
								texCache.groups.Push( groups[ j ] );
						}

						groupcache.Push( texCache );
					}

					// Spawning actor(s) according to the cached groups:
					for ( int j = 0; j < texCache.groups.Size(); j++ ) {
						vector2 newActorXY = ( l.linedef.v1.p + l.linedef.v2.p ) / 2;
						vector3 newActorPos = ( newActorXY.x, newActorXY.y, ( l.sector.ceilingplane.d - l.sector.floorplane.d ) / 2 );

						texCache.groups[ j ].SpawnActor( newActorPos );
					}
				} // of if ( texID ) {

			} // of for ( int i = 0; i < 2; i++ ) {

		} // of for ( int curSideIndex = 0; curSideIndex < level.Sides.Size(); curSideIndex++ ) {


		Super.WorldLoaded( e );
	} // of override void WorldLoaded( WorldEvent e ) {

} // of class AmbientHandler: EventHandler {


class AutoAmbientActor_Comp: Actor {
	Default {
		+NOINTERACTION;
		+NOBLOCKMAP;
	}

	States {
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SetTics( Random( 0, 29 ) );
		TNT1 A 0 A_Jump( 127, "Spawn2" );
		TNT1 A 30 A_PlaySound( "AA/Computers/Beep", CHAN_AUTO );
		Wait;
	Spawn2:
		TNT1 A 30 A_PlaySound( "AA/Computers/Boop", CHAN_AUTO );
		Wait;
	}
}

class AutoAmbientActor_Pipe: AutoAmbientActor_Comp {
	States {
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SetTics( Random( 0, 69 ) );
		TNT1 A -1 A_PlaySound( "AA/Pipes", CHAN_AUTO, looping: true );
		Stop;
	}
}

class AutoAmbientActor_Tech: AutoAmbientActor_Comp {
	States {
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SetTics( Random( 0, 69 ) );
		TNT1 A -1 A_PlaySound( "AA/Tech", CHAN_AUTO, looping: true );
		Stop;
	}
}

class AutoAmbientActor_BloodPipe: AutoAmbientActor_Comp {
	States {
	Spawn:
		TNT1 A 0;
		TNT1 A 1 A_SetTics( Random( 0, 69 ) );
		TNT1 A -1 A_PlaySound( "AA/BloodPipes", CHAN_AUTO, looping: true );
		Stop;
	}
}
